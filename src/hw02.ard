\import Data.Bool
\import Data.List
\import Data.Or
\import Function.Meta
\import Logic

-- 1. Определите полиморфный tail для списков.

\func tail {A : \Type} (_ : List A) : List A
  | nil => nil -- ???
  | _ :: xs => xs

\func testTail1 : tail {Nat} nil = nil => idp
\func testTail2 : tail (1 :: 2 :: nil) = 2 :: nil => idp

-- 2. Реализуйте любой (but choose wisely) алгоритм сортировки. Функцию сравнения можно принимать в качестве аргумента.

\func sort {A : \Type} (cmp : A -> A -> Bool) (xs : List A) : List A \elim xs
  | nil => nil
  | a :: l => insert a $ sort cmp l
  \where {
    \func insert (e : A) (xs : List A) : List A \elim xs
      | nil => e :: nil
      | a :: xs \as l => if (cmp e a) (e :: l) (a :: insert e xs)
  }

\func \infix 4 < (_ _ : Nat) : Bool
  | 0, 0 => false
  | 0, suc y => true
  | suc x, 0 => false
  | suc x, suc y => x < y

\func sortTest1 : sort (<) (1 :: 3 :: 2 :: nil) = 1 :: 2 :: 3 :: nil => idp
\func sortTest2 : sort (<) nil = nil => idp
\func sortTest3 : sort (<) (3 :: 2 :: 1 :: nil) = 1 :: 2 :: 3 :: nil => idp
\func sortTest4 : sort (<) (1 :: 2 :: 3 :: nil) = 1 :: 2 :: 3 :: nil => idp

-- 3. Докажите ассоциативность && и ||.

\func &&-assoc (x y z : Bool) : (x and y) and z = x and (y and z) \elim x, y
  | false, _ => idp
  | true, false => idp
  | true, true => idp

\func ||-assoc (x y z : Bool) : (x or y) or z = x or (y or z) \elim x, y
  | true, _ => idp
  | false, true => idp
  | false, false => idp

-- 4. Докажите, что fac 3 равно 6.

\func fac (_ : Nat) : Nat
  | 0 => 1
  | suc n \as x => x Nat.* fac n

\func facTest : fac 3 = 6 => idp

-- 5. Докажите следующие пропозициональные тавтологии (choose 2 out of 3).

-- t1 : (P and Q -> R) -> P -> Q -> R
\func t1 {P Q R : \Type} (f : \Sigma P Q -> R) (p : P) (q : Q) : R => f (p, q)

-- t2 : (P -> R) -> (Q -> R) -> P or Q -> R
\func t2 {P Q R : \Type} (f : P -> R) (g : Q -> R) (h : Or P Q) : R \elim h
  | inl p => f p
  | inr q => g q

-- t3 : ((P -> Q -> R) -> P) -> (P -> R) -> R
\func t3 {P Q R : \Type} (f : (P -> Q -> R) -> P) (g : P -> R) : R => g $ f $ \lam x _ => g x

-- 6. Сформулируйте и докажите следующие пропозициональные тавтологии (choose 2 out of 3).

-- t4 : (P -> Q -> R) -> P and Q -> R
\func t4 {P Q R : \Type} (f : P -> Q -> R) (pq : \Sigma P Q) : R => f pq.1 pq.2

-- t5 : (P or Q -> P and Q) -> (P -> Q) and (Q -> P)
\func t5 {P Q : \Type} (f : Or P Q -> \Sigma P Q) : \Sigma (P -> Q) (Q -> P) =>
  (\lam x => (f $ inl x).2, \lam x => (f $ inr x).1)

-- t6 : ((((P -> Q) -> P) -> P) -> Q) -> Q

-- 7. Докажите следующие утверждения.

-- s1 : Если для любого x : Nat верно P(x), то существует x : Nat для которого P(x) верно.
\func s1 (P : Nat -> \Type) (h : \Pi (x : Nat) -> P x) : \Sigma (x : Nat) (P x) => (42, h 42)

-- s2 : Если не существует x : Nat такого, что верно P(x), то P(3) не верно.
\func s2 (P : Nat -> \Type) (h : \Sigma (x : Nat) (P x) -> Empty) : P 3 -> Empty => \lam a => h (3, a)

-- 8. Сформулируйте и докажите следующие утверждения.

-- s3 : Если для всех x : Nat верно, что P(x) влечет Q(x), и существует x : Nat для которого верно P(x), то существует x : Nat для которого верно Q(x).
\func s3 (P Q : Nat -> \Type) (h : \Pi (x : Nat) -> P x -> Q x) (e : \Sigma (x : Nat) (P x)) : \Sigma (x : Nat) (Q x) => (e.1, h e.1 e.2)

-- s4 : Если для всех x : Nat либо не верно P(x), либо не верно Q(x), то P(3) влечет, что Q(3) не верно.
\func s4 (P Q : Nat -> \Type) (h : \Pi (x : Nat) -> Or (P x -> Empty) (Q x -> Empty)) : P 3 -> Q 3 -> Empty =>
  \case h 3 \with {
    | inl !p3 => \lam x _ => !p3 x
    | inr !q3 => \lam _ => !q3
  }
